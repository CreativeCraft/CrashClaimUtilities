plugins {
  id 'com.github.johnrengelman.shadow' version '8.1.1'
  id 'java'
}

group = 'org.creativecraft'
version = '1.0.5'

java {
  sourceCompatibility = JavaVersion.VERSION_16
  targetCompatibility = JavaVersion.VERSION_16
}

repositories {
  mavenCentral()
  maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/public/' }
  maven { url = 'https://oss.sonatype.org/content/groups/public/' }
  maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
  maven { url = 'https://repo.minebench.de/' }
  maven { url = 'https://maven.enginehub.org/repo/' }
  flatDir {
    dirs 'libs'
  }
}

dependencies {
  compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'
  compileOnly fileTree(dir: 'libs', include: '*.jar')
  implementation 'co.aikar:acf-bukkit:0.5.1-SNAPSHOT'
  implementation 'de.themoep:minedown:1.7.1-SNAPSHOT'
  implementation 'org.bstats:bstats-bukkit-lite:1.8'
}

shadowJar {
  relocate 'co.aikar.commands', 'org.creativecraft.crashclaimutilities.lib.acf'
  relocate 'co.aikar.locales', 'org.creativecraft.crashclaimutilities.lib.locales'
  relocate 'de.themoep.minedown', 'org.creativecraft.crashclaimutilities.lib.minedown'
  relocate 'org.bstats', 'org.creativecraft.crashclaimutilities.lib.bstats'
}

processResources {
  def props = [version: version]
  inputs.properties props
  filteringCharset 'UTF-8'
  filesMatching('plugin.yml') {
    expand props
  }
}

build.dependsOn shadowJar
